defaultTasks 'build'

buildscript {
    ext.kotlin_version = '1.3.11'
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.github.johnrengelman.shadow"

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

def getVersionName() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    def output = stdout.toString().trim()
    output = output.replaceFirst(/([\d.]+)/, '$1')
    output = output.replaceFirst(/([\d.]+)-(\d+)-.*/, '$1-SNAPSHOT')
    return output
}

task printVersion {
    doLast {
        println("version = ${getVersionName()}")
    }
}

group = 'ru.acapella.db'
version = getVersionName()

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

test {
    testLogging.showStandardStreams = true
}

sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'io.grpc', name: 'grpc-netty-shaded', version: '1.17.1'
    compile group: 'ru.acapella.db', name: 'adb-api-grpc', version: '0.2.0-SNAPSHOT'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.1.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.1.0'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'AcapellaDB JDBC driver'
        attributes 'Implementation-Version': version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    configurations = ['archives']
    pkg {
        repo = 'acapella-db'
        name = 'jdbc-driver'
        userOrg = 'acapellasoft'
        vcsUrl = 'https://github.com/AcapellaSoft/adb-jdbc-driver.git'
        licenses = ['Apache-2.0']
        version {
            name = project.version
            desc = 'AcapellaDB JDBC driver'
            released  = new Date()
            vcsTag = "$project.version"
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

shadowJar {
    classifier = null
    version = null
}
